Use cApplication.pkg
Use seq_chnl.pkg

Class cFileListHander is a cObject
    
    Procedure CreateNewFileList
        Handle  hoWorkspace hoFileHandler
        String  sDataPath sPathAtIndex
        Integer iPathsCount iPath iSupported iCh
        Boolean bExists bError
        
        Get phoWorkspace of ghoApplication to hoWorkspace
        Get psDataPath of hoWorkspace to sDataPath
        Get CountOfPaths of hoWorkspace sDataPath to iPathsCount
        For iPath from 1 to iPathsCount
            Get PathAtIndex of hoWorkspace sDataPath iPath to sPathAtIndex
            If (Right(sPathAtIndex,1)=SysConf(SYSCONF_DIR_SEPARATOR)) Begin
                Move (Left(sPathAtIndex,Length(sPathAtIndex)-1)) to sPathAtIndex
            End
            Move (DoesFileExist(sPathAtIndex)) to bExists
            If (not(bExists)) Begin
                Make_Directory sPathAtIndex
                Move (DoesFileExist(sPathAtIndex)) to bExists
                Move (not(bExists)) to bError
            End
            If (not(bError)) Begin
                // we only need one filelist and it should go in the first identified path
                If (iPath=1) Begin
                    Move (Seq_New_Channel()) to iCh
                    Direct_Output channel iCh (sPathAtIndex-'\filelist.cfg')
                    Write channel iCh "filelist.cfg"
                    Close_Output channel iCh
                    Send Seq_Release_Channel iCh
 
                    Set_Attribute DF_FILELIST_NAME to (sPathAtIndex-'\filelist.cfg')
                    Get_Attribute DF_NUMBER_FILES_SUPPORTED to iSupported
                    Set_Attribute DF_FILE_ROOT_NAME    of iSupported to ""
                    Set_Attribute DF_FILE_DISPLAY_NAME of iSupported to ""
                    Set_Attribute DF_FILE_LOGICAL_NAME of iSupported to "" 
                End
            End
        Loop
    End_Procedure
    
    Function IsEmpty Returns Boolean
        Handle hTable
        Get_Attribute DF_FILE_NEXT_USED of hTable to hTable
        Function_Return (hTable=0)  // returns true if there is no entry in the filelist.cfg
    End_Function 
End_Class